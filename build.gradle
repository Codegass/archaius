buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.netflix.nebula.netflixoss' version '11.1.1'
}

// Establish version and status
ext.githubProjectName = rootProject.name

subprojects {
    apply plugin: 'com.netflix.nebula.netflixoss'
    apply plugin: 'java'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
    }
    
    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.slf4j:slf4j-reload4j:1.7.36'
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
        testImplementation 'org.mockito:mockito-core:4.+'
    }

    group = "com.netflix.${githubProjectName}"

    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }

    tasks.withType(Test) {
        var testJdk = System.getProperty("testJdk", "8")
        System.console().printf("Launching tests using JDK %s%n", testJdk)
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(testJdk)
        }
        if (javaLauncher.get().metadata.languageVersion.asInt() >= 11) {
            // Mockito 4 will not work on JDK >=11 without this. Mockito 5 does not work on JDK8 at all :-/
            jvmArgs '--add-opens', 'java.base/java.lang=ALL-UNNAMED'
        }
    }
}
